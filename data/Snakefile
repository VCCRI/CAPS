from snakemake.remote.GS import RemoteProvider

GS = RemoteProvider(stay_on_remote=True)

syn_by_context = GS.remote(config["gcp_rootdir"] + "syn_by_context.tsv")
intronic_by_context = GS.remote(config["gcp_rootdir"] + "intronic_by_context.tsv")
all_by_csq = GS.remote(config["gcp_rootdir"] + "by_csq.tsv")


rule all:
    input:
        intronic_by_context,
        syn_by_context,
        all_by_csq,


# Counts of singletons in intronic variants grouped by
# context with a unique mutability value
rule intronic_by_context:
    output:
        intronic_by_context,
    run:
        import hail as hl
        from preprocessing import preprocessing

        # QC
        intron_vars = preprocessing(
            config["exomes_ht_path"],
            config["context_ht_path"],
            {"female": config["female"], "male": config["male"]},
            "intron_variant",
        )

        # Group by context
        intron_vars_grouped = intron_vars.group_by(
            "context",
            "ref",
            "alt",
            "variant_type",
            "methylation_level",
        ).aggregate(
            variant_count=hl.agg.count(),
            singleton_count=hl.agg.count_where(intron_vars.freq[0].AC == 1),
        )

        # Annotate with mutation rates (mutability)
        mutation_ht = hl.read_table(config["mutation_rate_ht_path"])
        intron_vars_grouped = intron_vars_grouped.annotate(
            mu=mutation_ht[
                hl.struct(
                    context=intron_vars_grouped.context,
                    ref=intron_vars_grouped.ref,
                    alt=intron_vars_grouped.alt,
                    methylation_level=intron_vars_grouped.methylation_level,
                )
            ].mu_snp
        )


        intron_vars_grouped.export(output[0])


# Counts of singletons in synonymous variants grouped by
# context with a unique mutability value
rule syn_by_context:
    output:
        syn_by_context,
    run:
        import hail as hl
        from preprocessing import preprocessing

        # QC
        syn_vars = preprocessing(
            config["exomes_ht_path"],
            config["context_ht_path"],
            {"female": config["female"], "male": config["male"]},
            "synonymous_variant",
        )

        # Group by context
        syn_vars_grouped = syn_vars.group_by(
            "context",
            "ref",
            "alt",
            "variant_type",
            "methylation_level",
        ).aggregate(
            variant_count=hl.agg.count(),
            singleton_count=hl.agg.count_where(syn_vars.freq[0].AC == 1),
        )

        # Annotate with mutation rates (mutability)
        mutation_ht = hl.read_table(config["mutation_rate_ht_path"])
        syn_vars_grouped = syn_vars_grouped.annotate(
            mu=mutation_ht[
                hl.struct(
                    context=syn_vars_grouped.context,
                    ref=syn_vars_grouped.ref,
                    alt=syn_vars_grouped.alt,
                    methylation_level=syn_vars_grouped.methylation_level,
                )
            ].mu_snp
        )


        syn_vars_grouped.export(output[0])


# Counts of singletons in all variants grouped by
# variant class and context
rule all_by_csq:
    output:
        all_by_csq,
    run:
        import hail as hl
        from misc import *

        exomes = hl.read_table(config["exomes_ht_path"])

        # Allele number (AN) adjustment.
        exomes = exomes.filter(
            get_an_adj_criteria(
                exomes, {"female": config["female"], "male": config["male"]}
            )
        )

        exomes = exomes.filter((exomes.freq[0].AF > 0) & (exomes.filters.length() == 0))

        context = hl.read_table(config["context_ht_path"])
        context = context[exomes.key]

        exomes = prepare_ht(
            exomes.annotate(context=context.context, methylation=context.methylation),
            trimer=True,
            annotate_coverage=False,
        )

        mutation_rates = hl.read_table(config["mutation_rate_ht_path"])
        exomes = exomes.annotate(
            mu=mutation_rates[
                hl.struct(
                    context=exomes.context,
                    ref=exomes.ref,
                    alt=exomes.alt,
                    methylation_level=exomes.methylation_level,
                )
            ].mu_snp
        )

        exomes = exomes.annotate(worst_csq=exomes.vep.most_severe_consequence)

        exomes.group_by(
            "context", "ref", "alt", "methylation_level", "mu", "worst_csq"
        ).aggregate(
            variant_count=hl.agg.count(),
            singleton_count=hl.agg.count_where(exomes.freq[0].AC == 1),
        ).export(
            output[0]
        )
